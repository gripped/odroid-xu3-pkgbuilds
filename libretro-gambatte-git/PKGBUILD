# Maintainer:  prettyvanilla <prettyvanilla@posteo.at>
# Contributor: almostalive   <almostalive2003 at gmail dot com>

pkgname=libretro-gambatte-git
pkgver=578.f5f8a19
pkgrel=1
pkgdesc="libretro implementation of Gambatte. (Game Boy/Game Boy Color)"
arch=('i686' 'x86_64' 'arm' 'armv6h')
url="https://github.com/libretro/gambatte-libretro"
license=('GPL')
makedepends=('git')

_gitname=gambatte-libretro
source=("git+https://github.com/libretro/${_gitname}.git"
        "https://raw.github.com/libretro/libretro-super/master/dist/info/gambatte_libretro.info")
md5sums=('SKIP'
         'SKIP')

pkgver() {
  cd "${_gitname}"
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

build() {
  cd "${_gitname}/libgambatte"
  sed -i 's:CFLAGS += -O3:CFLAGS += -O2:g' Makefile.libretro
  make -f Makefile.libretro
}

package() {
  install -Dm644 "${_gitname}/libgambatte/gambatte_libretro.so" "${pkgdir}/usr/lib/libretro/libretro-gambatte.so"
  install -Dm644 "gambatte_libretro.info" "${pkgdir}/usr/lib/libretro/libretro-gambatte.info"
}
