# $Id: PKGBUILD 198221 2013-10-30 13:32:47Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: dorphell <dorphell@archlinux.org>
# Contributor: Travis Willard <travis@archlinux.org>
# Contributor: Douglas Soares de Andrade <douglas@archlinux.org>
# Modifier; gripped - forum.odroid.com

pkgname=libpng-neon
downname=libpng
pkgver=1.6.19
_apngver=1.6.19
pkgrel=1
pkgdesc="A collection of routines used to create PNG format graphics files"
arch=('i686' 'x86_64')
url="http://www.libpng.org/pub/png/libpng.html"
license=('custom')
depends=('zlib' 'sh')
conflicts=('libpng')
provides=('libpng=${pkgver}')
options=('!libtool')
source=(http://downloads.sourceforge.net/sourceforge/$downname/$downname-$pkgver.tar.xz{,.asc}
        http://downloads.sourceforge.net/sourceforge/libpng-apng/libpng-$_apngver-apng.patch.gz)
md5sums=('1e6a458429e850fc93c1f3b6dc00a48f'
         'SKIP'
         'b215830867151242fb4ef9d246f050c4')

prepare() {
  cd $downname-$pkgver

  # Add animated PNG (apng) support. Required by Firefox
  # see http://sourceforge.net/projects/libpng-apng/
  patch -Np1 -i ../libpng-$_apngver-apng.patch
}

build() {
  cd $downname-$pkgver

  ./configure --prefix=/usr --disable-static --enable-arm-neon=check
  make
}

package() {
  cd $downname-$pkgver
  make DESTDIR="$pkgdir" install

  cd contrib/pngminus
  make PNGLIB="-L$pkgdir/usr/lib -lpng" -f makefile.std png2pnm pnm2png
  install -m755 png2pnm pnm2png "$pkgdir/usr/bin/"
  install -D -m644 ../../LICENSE "$pkgdir/usr/share/licenses/$downname/LICENSE"
}
